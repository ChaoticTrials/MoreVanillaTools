plugins {
    id 'org.moddingx.modgradle.meta' version '5.0.2'
}

repositories {
    maven {
        url = "https://modmaven.dev/"
    }
}

var baseVersion = '21.1'
var libxVersion = '1.21.1-6.0.0'
var jeiVersion = '19.21.0.247'
var morevanillalibVersion = '21.1.2'

mod.configure {
    modid 'morevanillatools'
    group 'de.melanx'
    versioning {
        base baseVersion
        maven 'https://maven.melanx.de/release'
    }

    neoforge "${baseVersion}.115"

    license 'The Apache License, Version 2.0'
    github 'ChaoticTrials/MoreVanillaTools'

    artifacts {
        sources {
            publishToRepositories()
        }
    }

    changelog {
        def lines = defaultChangelog.toString().split('\n')
        def filteredChangelog = lines.findAll { !it.startsWith('- [meta]') }.join('\n')

        return filteredChangelog
    }


    publishing {
        maven {
            name 'melanx'
            url 'https://maven.melanx.de/release'
            credentials(PasswordCredentials)
        }
    }

    upload {
        all {
            type 'release'
            require 'libx'
            optional 'morevanillalib'
            optional 'jei'
        }
        curseforge {
            projectId 349504
        }
        modrinth {
            projectId 'HJphSXip'
        }
    }
}

dependencies {
    annotationProcessor "org.moddingx:LibX:${libxVersion}"

    implementation "org.moddingx:LibX:${libxVersion}"
    implementation "de.melanx:MoreVanillaLib:${morevanillalibVersion}"
    implementation "mezz.jei:jei-1.${baseVersion}-neoforge:${jeiVersion}"
}
